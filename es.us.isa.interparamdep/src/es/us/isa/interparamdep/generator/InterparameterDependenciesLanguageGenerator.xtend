/*
 * generated by Xtext 2.19.0
 */
package es.us.isa.interparamdep.generator

import org.eclipse.emf.ecore.resource.Resource

import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext

import java.io.File
import java.util.Map
import java.util.HashMap
import java.io.BufferedWriter
import java.io.FileWriter
import com.fasterxml.jackson.databind.ObjectMapper

import static es.us.isa.interparamdep.generator.ReservedWords.RESERVED_WORDS;

import es.us.isa.interparamdep.interparameterDependenciesLanguage.GeneralClause
import es.us.isa.interparamdep.interparameterDependenciesLanguage.GeneralPredefinedDependency
import es.us.isa.interparamdep.interparameterDependenciesLanguage.Dependency;
import es.us.isa.interparamdep.interparameterDependenciesLanguage.ConditionalDependency;
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.ConditionalDependencyImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.GeneralPredefinedDependencyImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.ArithmeticDependencyImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.ArithmeticDependency
import es.us.isa.interparamdep.interparameterDependenciesLanguage.Operation
import es.us.isa.interparamdep.interparameterDependenciesLanguage.Param
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.ParamImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.OperationContinuation
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.RelationalDependencyImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.RelationalDependency
import es.us.isa.interparamdep.interparameterDependenciesLanguage.impl.GeneralTermImpl
import es.us.isa.interparamdep.interparameterDependenciesLanguage.GeneralTerm
import es.us.isa.interparamdep.interparameterDependenciesLanguage.GeneralPredicate

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class InterparameterDependenciesLanguageGenerator extends AbstractGenerator {
	
	val String constraintsFilePath = "./idl_aux_files/base_constraints.mzn"
	val String stringIntMappingFilePath = "./idl_aux_files/string_int_mapping.json"
	var String csp
	var String fullCsp
	var Integer stringToIntCounter
	var Map<String, Integer> stringIntMapping = new HashMap

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {

		var file = new File(constraintsFilePath)
		file.delete
		if (!file.exists) {
		  file.parentFile.mkdirs
		  file.createNewFile
		}
		var BufferedWriter out = new BufferedWriter(new FileWriter(file, false))
		
		stringIntMapping.clear
		stringToIntCounter = 0
		
		fullCsp = "constraint ("
		for (dependency: resource.allContents.filter(Dependency).toIterable) {
			csp = "("
			if (dependency.dep.class == typeof(ConditionalDependencyImpl)) {
				writeConditionalDependency(dependency.dep as ConditionalDependency)
			} else if (dependency.dep.class == typeof(RelationalDependencyImpl)) {
				writeRelationalDependency(dependency.dep as RelationalDependency, true)
			} else if (dependency.dep.class == typeof(ArithmeticDependencyImpl)) {
				writeArithmeticDependency(dependency.dep as ArithmeticDependency, true)
			} else if (dependency.dep.class == typeof(GeneralPredefinedDependencyImpl)) {
				writePredefinedDependency(dependency.dep as GeneralPredefinedDependency)
			} else {
				throw new Exception("The dependency must be a conditional, an " + 
					"arithmetic, a relational or a predefined one")
			}
			csp += ")\n/\\\n"
			
			fullCsp += csp
			
		}
		fullCsp = fullCsp.substring(0, fullCsp.length-4)
		fullCsp += ");"
		if (fullCsp.contains("constrai);"))
			fullCsp = ""
		
		out.append(fullCsp)
		out.flush
		out.close
		
		// Export string-int mapping to JSON
		var ObjectMapper mapper = new ObjectMapper
		var String json = mapper.writerWithDefaultPrettyPrinter.writeValueAsString(stringIntMapping)
		var mappingFile = new File(stringIntMappingFilePath)
		mappingFile.delete
		if (!mappingFile.exists) {
		  mappingFile.parentFile.mkdirs
		  mappingFile.createNewFile
		}
        var BufferedWriter mappingOut = new BufferedWriter(new FileWriter(mappingFile, false))
        mappingOut.append(json)
        mappingOut.flush
        mappingOut.close
	}
	
	
	/**
	 * Remove and replace special characters from paramName
	 */
	def String parseIDLParamName(String paramName) {
		var String parsedParamName = paramName.replaceAll("^\\[|\\]$", "").replaceAll("[\\.\\-\\/\\:\\[\\]]", "_")
		if (RESERVED_WORDS.contains(parsedParamName))
			parsedParamName += "_R"
		return parsedParamName
	}

	def Integer stringToInt(String stringValue) {
		val Integer intMapping = stringIntMapping.get(stringValue)
		if (intMapping !== null) {
			return intMapping
		} else {
			stringIntMapping.put(stringValue, stringToIntCounter)
			return stringToIntCounter++
		}
	}
	
	
	/**
	 * Surround double with brackets if it's negative, and remove decimals (MiniZinc does not support floats)
	 */
	def String parseDouble(String doubleValue) {
		val doubleWithoutDec = doubleValue.replaceAll("\\.\\d+", "")
		if (doubleWithoutDec.contains('-'))
			return ('(' + doubleWithoutDec + ')')
		return doubleWithoutDec
	}
	
	/**
	 * Returns true if param is actually a ParamValueRelation. False if it is a Param
	 */
	def boolean isParamValueRelation(Param param) {
		return param.stringValues.size !== 0 || param.patternString !== null || param.booleanValue !== null || param.doubleValue !== null
	}
	
	def void writePredicate(GeneralPredicate predicate) {
		writeClause(predicate.firstClause)
		
		if (predicate.clauseContinuation !== null) {
			// Solve second element, which is a clause continuation containing a predicate
			if (predicate.clauseContinuation.logicalOp == "AND") {
				csp += " /\\ "
			} else if (predicate.clauseContinuation.logicalOp == "OR") {
				csp += " \\/ "
			} else {
				throw new Exception("The logical operator can only be AND or OR")
			}
			writePredicate(predicate.clauseContinuation.additionalElements)
		}
	}
	
	def void writeClause(GeneralClause clause) {
		if (clause.predicate !== null) {
			if (clause.not !== null)
				csp += "(not "
			csp += "("
			writePredicate(clause.predicate)
			csp += ")"
			if (clause.not !== null)
				csp += ")"
		}
		
		// Solve firstElement, which can be a term, arithmetic dep, relational dep or predefined dep
		if (clause.firstElement !== null) {
			if (clause.firstElement.class == typeof(GeneralTermImpl)) { // param or param assignment
				val GeneralTerm term = (clause.firstElement as GeneralTerm)
				val Param param = (term.param as Param)
				
				if (term.not !== null)
					csp += "(not "
				csp += "("
	
				csp += parseIDLParamName(param.name) + "Set==1"
				
				if (isParamValueRelation(param)) {
					csp += " /\\ "
					if (param.booleanValue !== null) {
						csp += parseIDLParamName(param.name) + "==" + param.booleanValue
					} else if (param.doubleValue !== null) {
						csp += parseIDLParamName(param.name) + param.relationalOp + parseDouble(param.doubleValue)
					} else if (param.stringValues.size !== 0) {
						csp += "("
						for (string: param.stringValues) {
							csp += parseIDLParamName(param.name) + "==" + stringToInt(string) + " \\/ "
						}
						csp = csp.substring(0, csp.length-4) // Trim last " \\/ "
						csp += ")"
					} else if (param.patternString !== null) {
						// TODO: Implement CSP mapping (none for now)
						csp += "true"
					}
				}
				csp += ")"
				if (term.not !== null)
					csp += ")"
			} else if(clause.firstElement.class == typeof(RelationalDependencyImpl)) {
				writeRelationalDependency(clause.firstElement as RelationalDependency, false)
			} else if(clause.firstElement.class == typeof(ArithmeticDependencyImpl)) {
				writeArithmeticDependency(clause.firstElement as ArithmeticDependency, false)
			} else if(clause.firstElement.class == typeof(GeneralPredefinedDependencyImpl)) {
				writePredefinedDependency(clause.firstElement as GeneralPredefinedDependency)
			} else {
				throw new Exception("The first element of a clause must be a term, an " + 
					"arithmetic dependency, a relational dependency or a predefined dependency")
			}
		}
	}
	
	def void writeConditionalDependency(ConditionalDependency dep) {
		csp += "("
		writePredicate(dep.condition)
		csp += ") -> ("
		writePredicate(dep.consequence)
		csp += ")"
	}

	def void writeRelationalDependency(RelationalDependency dep, boolean alone) {
		if (alone)
			csp += "((" + parseIDLParamName(dep.param1.name) + "Set==1 /\\ " + parseIDLParamName(dep.param2.name) + "Set==1) -> (" +
					parseIDLParamName(dep.param1.name) + dep.relationalOp + parseIDLParamName(dep.param2.name) + "))"
		else
			csp += "(" + parseIDLParamName(dep.param1.name) + "Set==1 /\\ " + parseIDLParamName(dep.param2.name) + "Set==1 /\\ " +
					parseIDLParamName(dep.param1.name) + dep.relationalOp + parseIDLParamName(dep.param2.name) + ")"
	}
	
	def void writeArithmeticDependency(ArithmeticDependency dep, boolean alone) {
		csp += "(("
		for (param: dep.eAllContents.filter(Param).toIterable) {
			csp += parseIDLParamName(param.name) + "Set==1 /\\ "
		}
		if (alone) {
			csp = csp.substring(0, csp.length-4) // Trim last " /\\ "
			csp += ") -> ("
		}
		writeOperation(dep.operation)
		csp += dep.relationalOp
		csp += parseDouble(dep.result)
		csp += "))"
	}
	
	def void writeOperation(Operation operation) {
		if (operation.openingParenthesis === null) { // Alternative 1 of Operation
			csp += operation.firstParam.name
			writeOperationContinuation(operation.operationContinuation)
			
		} else { // Alternative 2 of Operation
			csp += "("
			writeOperation(operation.operation)
			csp += ")"
			if (operation.operationContinuation !== null) {
				writeOperationContinuation(operation.operationContinuation)
			}
			
		}
	}
	
	def void writeOperationContinuation(OperationContinuation opCont) {
		csp += opCont.arithOp
		if (opCont.additionalParams.class == typeof(ParamImpl)) {
			csp += (opCont.additionalParams as Param).name
		} else {
			writeOperation(opCont.additionalParams as Operation)
		}
	}
	
	def void writePredefinedDependency(GeneralPredefinedDependency dep) {		
		if (dep.not !== null)
			csp += "(not "
		csp += "("
		
		for (depElement: dep.predefDepElements) {
			csp += "("
			switch dep.predefDepType {
				case "Or": {
					writePredicate(depElement)
					csp += ") \\/ "
				} case "OnlyOne": {
					writeZeroOrOneOnlyOneElement(depElement, dep.predefDepElements)
					csp += ") /\\ "
				} case "AllOrNone": {
					writeAllOrNoneElement(depElement, dep.predefDepElements)
					csp += ") /\\ "
				} case "ZeroOrOne": {
					writeZeroOrOneOnlyOneElement(depElement, dep.predefDepElements)
					csp += ") /\\ "
				} default:
					throw new Exception("The predefined dependency can only be 'Or', " + 
						"'OnlyOne', 'AllOrNone' or 'ZeroOrOne'")
			}
		}
		if (dep.predefDepType.equals("OnlyOne")) { // If dep is OnlyOne, one more clause must be added
			csp += "("
			for (depElement: dep.predefDepElements) {
				csp += "("
				writePredicate(depElement)
				csp += ") \\/ "
			}
			csp = csp.substring(0, csp.length-4) // Trim last " /\\ " or " \\/ "
			csp += ")"
		} else { // Otherwise, last logicalOp must be removed
			csp = csp.substring(0, csp.length-4) // Trim last " /\\ " or " \\/ "
		}
		csp += ")"
		if (dep.not !== null)
			csp += ")"
	}
		
	def void writeZeroOrOneOnlyOneElement(GeneralPredicate element, GeneralPredicate[] allElements) {
		writeZeroOrOneAllOrNoneElement(element, allElements, false, true)
	}
	
	def void writeAllOrNoneElement(GeneralPredicate element, GeneralPredicate[] allElements) {
		writeZeroOrOneAllOrNoneElement(element, allElements, false, false)
		csp += ") /\\ ("
		writeZeroOrOneAllOrNoneElement(element, allElements, true, true)
	}
	
	def void writeZeroOrOneAllOrNoneElement(GeneralPredicate element, GeneralPredicate[] allElements, boolean negateElement, boolean negateRemainingElements) {
		if (negateElement) { // For AllOrNone dependencies
			csp += "(not ("
			writePredicate(element)
			csp += ")) -> ("
		} else { // For ZeroOrOne, AllOrNone and OnlyOne dependencies
			csp += "("
			writePredicate(element)
			csp += ") -> ("
		}
		for (remainingElement: allElements) {
			if (!remainingElement.equals(element)) { // Include remaining elements in the consequence
				if (negateRemainingElements) { // For ZeroOrOne dependencies
					csp += "(not ("
					writePredicate(remainingElement)
					csp += ")) /\\ "
				} else { // For ZeroOrOne, AllOrNone and OnlyOne dependencies
					csp += "("
					writePredicate(remainingElement)
					csp += ") /\\ "
				}
			}
		}
		csp = csp.substring(0, csp.length-4) // Trim last " /\\ "
		csp += ")"
	}
	
}



